buildscript {
    repositories {
        mavenCentral()
        maven { url "https://dl.bintray.com/kotlin/dokka" }
        maven { url "https://dl.bintray.com/kotlin" }
        maven { url "https://plugins.gradle.org/m2" }
        maven { url "https://dl.bintray.com/kotlin/kotlinx" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlint_gradle_version"
    }
}

task version {
    doLast {
        println project.version
    }
}

static getCurrentVersion() {
    def tag = System.getenv("CI_COMMIT_TAG")
    if (tag != null && tag != "") {
        return tag
    }
    return "0.0.1.13"
}

allprojects {
    group = "io.donesky.kdbi"
    version = getCurrentVersion()

    repositories {
	    mavenCentral()
        maven {
            url "https://artifacts.donesky.dev/donesky-all"
            credentials {
                username System.getenv("donesky_nexus_username")
                password System.getenv("donesky_nexus_password")
            }
        }
        mavenCentral()
        maven { url "https://dl.bintray.com/kotlin/dokka" }
        maven { url "https://dl.bintray.com/kotlin/kotlinx" }
    }

    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "maven-publish"
    apply plugin: "signing"

    task sourcesJar(type: Jar) {
        from project.file("src")
        archiveClassifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        archiveClassifier = 'javadoc'
    }

    artifacts {
        archives jar
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = project.group
                artifactId = project.name
                version = project.version

                from components.java
                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = "KDBI"
                    description = "An open source kotlin jdbc interface"
                    url = "https://donesky.io/kdbi"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = "kylebrock"
                            name = "Kyle Brock"
                            email = "brock@donesky.io"
                        }
                    }
                    scm {
                        connection = "scm:git:git://github.com/doneskyio/kdbi.git"
                        developerConnection = "scm:git:git://source.donesky.dev/donesky/kdbi.git"
                        url = "https://donesky.dev"
                    }
                }
            }
        }
        repositories {
            maven {
                if (System.getenv("sonatype_username") != null) {
                    url = version.endsWith('SNAPSHOT') ? "https://oss.sonatype.org/content/repositories/snapshots" : "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                    credentials {
                        username System.getenv("sonatype_username")
                        password System.getenv("sonatype_password")
                    }
                } else {
                    url = "https://artifacts.donesky.dev/donesky"
                    credentials {
                        username System.getenv("donesky_nexus_username")
                        password System.getenv("donesky_nexus_password")
                    }
                }
            }
        }
    }

    if (System.getenv("sonatype_username") != null) {
        signing {
            sign publishing.publications.mavenJava
        }
    }

    apply plugin: "jacoco"
    apply plugin: "org.jetbrains.dokka"
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    dependencies {
        compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version")

        compile("ch.qos.logback:logback-classic:$logback_version")

        testCompile("org.jetbrains.kotlin:kotlin-test:$kotlin_version")
        testCompile("org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version")
    }

    jacoco {
        toolVersion = "0.8.5"
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
            allWarningsAsErrors = true
            freeCompilerArgs += ["-Xuse-experimental=kotlin.Experimental"]
        }
    }

    ext.moduleName = "donesky.${project.name}"

    compileJava {
        inputs.property("moduleName", moduleName)
        doFirst {
            options.compilerArgs = [
                    "--module-path", classpath.asPath
            ]
            classpath = files()
        }
    }

    javadoc {
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_11
    }

    dokka {
        outputFormat = "kotlin-website"
        outputDirectory = "$buildDir/docs"
        reportUndocumented = false
    }

    ktlint {
        version = "$ktlint_version"
        debug = false
        verbose = true
        android = false
        outputToConsole = true
        ignoreFailures = false
        enableExperimentalRules = true
        filter {
            exclude("**/generated/**")
            include("**/src/**")
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.setDestination(file("${buildDir}/reports/jacoco/report.xml"))
        html.enabled true
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}
